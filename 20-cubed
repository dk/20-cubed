#!env perl
use strict;
use warnings;
use Prima qw(Classes Application);

my $sleep = 20 * 60;
my $tick  = 20;

my $dbm = Prima::Image->new(
	width  => 100,
	height => 100,
	monochrome => 1,
	color => cl::White,
	backColor => cl::Black,
);
$dbm->begin_paint;
$dbm->clear;
$dbm->fill_ellipse( 50, 50, 100, 100 );
$dbm->end_paint;

while ( 1 ) {
	sleep($sleep);
	my $t;
	my $sec = $tick;
	$t = Prima::Widget->new(
		size   => [100, 100],
		origin => [ 0, $::application-> height - 100 ],
		font   => { height => 80 },
		shape  => $dbm,
		onPaint => sub {
			$t->gradient_bar( 0, 0, $t->size, { palette => [ cl::Gray, cl::White ] });
			$t->color(cl::Black);
			$t->draw_text($sec, 0, 0, $t->size, dt::Center|dt::VCenter);
		},
		onMouseDown => sub {
			$t-> destroy;
			$sec = 0;
		},
	);
	$t-> insert( Timer => 
		timeout => 1000,
		onTick  => sub {	
			--$sec ? $t->repaint : $t->destroy;
		},
	)->start;

	$::application->yield while $sec > 0;
}
